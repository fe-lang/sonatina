#! evm.config: { emit_mem_plan: true }
#! evm.case: bug_demo
#!   calldata: u32be(11) u32be(22)
#!   expect: return u256(111)

# Regression: malloc ptr escapes via integer path
# `ptr_to_int -> evm_udiv -> non-local mstore`.
# Must still return 111 (previously miscompiled to 222).

target = "evm-ethereum-osaka"

func public %main() -> i256 {
    block0:
        v0.*i8 = evm_malloc 32.i256;
        v1.*i256 = bitcast v0 *i256;
        mstore v1 111.i256 i256;

        v2.i256 = ptr_to_int v0 i256;
        v3.i256 = evm_udiv v2 1.i256;
        mstore 0.i32 v3 i256;

        v4.*i8 = evm_malloc 32.i256;
        v5.*i256 = bitcast v4 *i256;
        mstore v5 222.i256 i256;

        v6.i256 = mload 0.i32 i256;
        v7.*i8 = int_to_ptr v6 *i8;
        v8.*i256 = bitcast v7 *i256;
        v9.i256 = mload v8 i256;
        return v9;
}

func public %entry() {
    block0:
        v0.i256 = call %main;
        mstore 0.i32 v0 i256;
        evm_return 0.i8 32.i8;
}

object @Contract {
  section runtime {
    entry %entry;
  }
}
