#! evm.config: { emit_mem_plan: true }
#! evm.case: bug_demo
#!   calldata: u32be(11) u32be(22)
#!   expect: return u256(111)

# Regression: malloc pointer escapes via non-local evm_mstore8 writes.
# Must remain valid across a later static-arena clobbering call
# (previously miscompiled to 999).

target = "evm-ethereum-osaka"

type @big = { i256, i256, i256, i256, i256, i256, i256, i256 };

func public %big() -> i256 {
    block0:
        v0.*@big = alloca @big;
        v1.*i256 = gep v0 0.i8 0.i8;
        mstore v1 999.i256 i256;
        v2.i256 = mload v1 i256;
        return v2;
}

func public %esc8() -> i256 {
    block0:
        v0.*i8 = evm_malloc 32.i256;
        v1.*i256 = bitcast v0 *i256;
        mstore v1 111.i256 i256;

        v2.i256 = ptr_to_int v0 i256;
        v3.i256 = shr 8.i256 v2;
        evm_mstore8 30.i32 v3;
        evm_mstore8 31.i32 v2;

        return 0.i256;
}

func public %main() -> i256 {
    block0:
        v9.i256 = call %esc8;
        v10.i256 = call %big;
        v0.i256 = mload 0.i32 i256;
        v1.*i8 = int_to_ptr v0 *i8;
        v2.*i256 = bitcast v1 *i256;
        v3.i256 = mload v2 i256;
        return v3;
}

func public %entry() {
    block0:
        v0.i256 = call %main;
        mstore 0.i32 v0 i256;
        evm_return 0.i8 32.i8;
}

object @Contract {
  section runtime {
    entry %entry;
  }
}
