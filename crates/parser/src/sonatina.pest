module = { SOI ~ NEWLINE* ~ target_specifier ~ (NEWLINE+ ~ declaration)* ~ (NEWLINE+ ~ function)* ~ (NEWLINE+ ~ object_definition)* ~ NEWLINE* ~ EOI }
object_file = { SOI ~ NEWLINE* ~ (object_definition ~ NEWLINE*)* ~ EOI }

WHITESPACE = _{ " " | "\t" }
COMMENT    =  { "#" ~ (!NEWLINE ~ ANY)* }

ident_start_char = { ASCII_ALPHA | "_" }
ident_body_char  = { ASCII_ALPHANUMERIC | "_" }

target_specifier = _{ "target" ~ "=" ~ "\"" ~ target_triple ~ "\"" }
target_triple    = @{ ASCII_ALPHA* ~ "-" ~ ASCII_ALPHA* ~ "-" ~ ASCII_ALPHA* }

declaration              = _{ function_declaration | struct_declaration | gv_declaration }
function_declaration     =  { "declare" ~ linkage ~ function_identifier ~ function_param_type_list ~ function_ret_type? ~ ";" }
function_param_type_list =  { "(" ~ (type_name ~ ",")* ~ type_name? ~ ")" }
struct_declaration       =  { "type" ~ struct_identifier ~ "=" ~ struct_fields ~ ";" }
struct_identifier        = ${ "@" ~ struct_name }
struct_fields            = _{ normal_field_list | packed_field_list }
normal_field_list        =  { "{" ~ type_list ~ "}" }
packed_field_list        =  { "<{" ~ type_list ~ "}>" }
type_list                = _{ (type_name ~ ",")* ~ type_name? }
struct_name              = @{ ident_start_char ~ ident_body_char* }


function            =  { function_signature ~ function_body }
_functions          = _{ (NEWLINE* ~ function ~ NEWLINE*)* }
function_signature  =  { "func" ~ linkage ~ function_identifier ~ function_params ~ function_ret_type? }
function_ret_type   =  { "->" ~ type_name }
linkage             =  { "public" | "private" | "external" }
function_identifier = ${ "%" ~ function_name }
function_name       = @{ ident_start_char ~ ident_body_char* }
function_params     =  { "(" ~ (value_declaration ~ ",")* ~ value_declaration? ~ ")" }
function_body       = _{ "{" ~ (NEWLINE+ ~ block?)* ~ "}" }
block               =  { block_ident ~ ":" ~ (NEWLINE+ ~ stmt)* }
_stmts              = _{ (stmt ~ NEWLINE+)* }

gv_declaration  =  { "global" ~ linkage ~ gv_const? ~ type_name ~ gv_identifier ~ ("=" ~ gv_initializer)?  ~ ";" }
gv_identifier   = ${ "$" ~ gv_name }
gv_name         = @{ ident_start_char ~ ident_body_char* }
gv_const        =  { "const" }
gv_initializer  =  { gv_value_imm | gv_value_array | gv_value_struct }
gv_value_imm    =  { number }
gv_value_array  =  { "[" ~ (gv_initializer ~ ",")* ~ gv_initializer? ~ "]" }
gv_value_struct =  { "{" ~ (gv_initializer ~ ",")* ~ gv_initializer? ~ "}" }

block_ident  = ${ "block" ~ block_number }
block_number =  { ASCII_DIGIT+ }
value_name   = ${ "v" ~ ASCII_DIGIT+ }

type_name          =  { primitive_type | ptr_type | array_type | unit_type | struct_identifier | function_type }
primitive_type     =  { "i8" | "i16" | "i32" | "i64" | "i128" | "i256" | "i1" }
ptr_type           = ${ "*" ~ type_name }
array_type         = !{ "[" ~ type_name ~ ";" ~ array_size ~ "]" }
array_size         =  { ASCII_DIGIT+ }
function_type      = !{ function_type_arg ~ function_ret_type }
function_type_arg  =  { "(" ~ type_list ~ ")" }
unit_type          =  { "unit" }

value_declaration = ${ value_name ~ "." ~ type_name }

// Object
object_definition   =  { "object" ~ object_identifier ~ object_body }
object_identifier   = ${ "@" ~ object_name }
object_name         = @{ ident_start_char ~ ident_body_char* }
object_body         = _{ "{" ~ (NEWLINE* ~ object_section ~ NEWLINE*)* ~ "}" }
object_section      =  { "section" ~ section_name ~ section_body }
section_name        = @{ ident_start_char ~ ident_body_char* }
section_body        = _{ "{" ~ (NEWLINE* ~ section_stmt ~ NEWLINE*)* ~ "}" }
section_stmt        =  { section_entry | section_include | section_data | section_embed }
section_entry       =  { "entry" ~ function_identifier ~ ";" }
section_include     =  { "include" ~ function_identifier ~ ";" }
section_data        =  { "data" ~ gv_identifier ~ ";" }
section_embed       =  { "embed" ~ section_ref ~ "as" ~ embed_symbol ~ ";" }
section_ref         =  { local_section_ref | external_section_ref }
local_section_ref   =  { "." ~ section_name }
external_section_ref = { object_identifier ~ "." ~ section_name }
embed_symbol        = ${ "&" ~ embed_name }
embed_name          = @{ ident_start_char ~ ident_body_char* }

// Stmts
stmt        = { (assign_stmt | inst_stmt) ~ ";" }
assign_stmt = { value_declaration ~ "=" ~ inst }
inst_stmt   = { inst }

inst            = { inst_name ~ inst_arg* }
inst_name       = { inst_identifier }
inst_identifier = @{ ident_start_char ~ ident_body_char* }
inst_arg        = { value | type_name | block_ident | value_block_map | function_identifier | embed_symbol }
value_block_map = { "(" ~ value ~ block_ident ~ ")" }

value        =  { value_name | imm_number | undef | global_value }
imm_number   = ${ number ~ "." ~ primitive_type }
global_value =  { gv_identifier }
undef        = ${ "undef" ~ "." ~ type_name }
number       = _{ hex | decimal }
decimal      = @{ "-"? ~ ASCII_DIGIT+ }
hex          = @{ "0x" ~ ASCII_HEX_DIGIT+ }
